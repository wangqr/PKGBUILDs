# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=aegisub-git
pkgver=3.2.2.r380.de2458817
pkgrel=1
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=(x86_64)
url=http://www.aegisub.org
license=(
  GPL
  BSD
)
depends=(
  alsa-lib
  boost-libs
  fftw
  fontconfig
  hunspell
  icu
  libass.so
  libffms2.so
  libgl
  libpulse
  uchardet
  wxgtk3
  zlib
)
makedepends=(
  autoconf-archive
  boost
  cmake
  git
  intltool
  lua
  mesa
)
provides=(aegisub)
conflicts=(aegisub)
source=(
  aegisub::git+https://github.com/wangqr/Aegisub.git
  git+https://github.com/Aegisub/assdraw.git
)
sha256sums=(
  SKIP
  SKIP
)

pkgver() {
  cd aegisub

  tag='v3.2.2'

  echo "${tag#v}.r$(git rev-list --count ${tag}..HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/aegisub"
  ./build/version.sh .

  mkdir -p "${srcdir}/build"
  cd "${srcdir}/build"
  cmake -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 -DCMAKE_INSTALL_PREFIX=/usr "${srcdir}/aegisub"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/aegisub"
  install -Dm 644 LICENCE -t "${pkgdir}"/usr/share/licenses/aegisub-git/
}

# vim: ts=2 sw=2 et:
